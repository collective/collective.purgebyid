varnishtest "Test xkey vmod"

server s1 {
	rxreq
	txresp -hdr "xkey: asdf"

} -start

varnish v1 -vcl+backend {
	import xkey;

	sub vcl_recv {
		if (req.http.xkey-purge) {
			if (xkey.purge(req.http.xkey-purge) != 0) {
				return (synth(200, "Purged"));
			} else {
				return (synth(404, "No key"));
			}
		}
	}

	sub vcl_backend_response {
		set beresp.ttl = 60s;
		set beresp.grace = 0s;
		set beresp.keep = 0s;
	}

	sub vcl_synth {
		set resp.http.reason = resp.reason;
	}
} -start

# First check xkey header
client c1 {
	txreq
	rxresp
} -run

varnish v1 -expect n_object == 1

client c1 {
	txreq -hdr "xkey-purge: asdf"
	rxresp
	expect resp.status == 200
	expect resp.http.reason == "Purged"
} -run

delay 1

varnish v1 -expect n_object == 0
